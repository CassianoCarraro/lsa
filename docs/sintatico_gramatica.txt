E' -> E
E -> INICIO
INICIO -> DEFINICAO_FUNCOES
INICIO -> DEFINICAO_VARIAVEIS

TIPO -> TK_VOID
TIPO -> TK_INT
TIPO -> TK_FLOAT
TIPO -> TK_DOUBLE

DEFINICAO_FUNCOES -> DEFINICAO_FUNCAO
DEFINICAO_FUNCOES -> DEFINICAO_FUNCAO DEFINICAO_FUNCOES
DEFINICAO_FUNCAO -> TIPO TK_ID TK_ABRE_PAR DEFINICAO_PARAMETROS TK_FECHAR_PAR DEFINICAO_FUNCAO_CORPO
DEFINICAO_FUNCAO -> TIPO TK_ID TK_ABRE_PAR TK_FECHAR_PAR DEFINICAO_FUNCAO_CORPO
DEFINICAO_FUNCAO_CORPO -> TK_PONTO_VIRG
DEFINICAO_FUNCAO_CORPO -> COMANDO_BLOCO

DEFINICAO_PARAMETROS -> DEFINICAO_PARAMETRO
DEFINICAO_PARAMETROS -> DEFINICAO_PARAMETRO TK_VIRGULA DEFINICAO_PARAMETROS
DEFINICAO_PARAMETRO -> TIPO TK_ID

DEFINICAO_VARIAVEIS -> DEFINICAO_VARIAVEL
DEFINICAO_VARIAVEIS -> DEFINICAO_VARIAVEL DEFINICAO_VARIAVEIS
DEFINICAO_VARIAVEL -> TIPO DEFINICAO_VARIAVEL_LISTA_ID TK_PONTO_VIRG
DEFINICAO_VARIAVEL_LISTA_ID -> DEFINICAO_VARIAVEL
DEFINICAO_VARIAVEL_LISTA_ID -> DEFINICAO_VARIAVEL TK_VIRGULA DEFINICAO_VARIAVEL_LISTA_ID
DEFINICAO_VARIAVEL -> TK_ID
DEFINICAO_VARIAVEL -> TK_ID TK_ATRIB EXPRESSAO

COMANDO -> COMANDO_ITERACAO
COMANDO -> COMANDO_EXPRESSAO
COMANDO -> COMANDO_PULO
COMANDO -> COMANDO_FUNCAO
COMANDO -> COMANDO_CONDICAO
COMANDO -> DEFINICAO_VARIAVEIS

COMANDO_BLOCO -> COMANDO
COMANDO_BLOCO -> TK_ABRE_CHAVE TK_FECHA_CHAVE
COMANDO_BLOCO -> TK_ABRE_CHAVE COMANDO_LISTA TK_FECHA_CHAVE

COMANDO_LISTA -> COMANDO
COMANDO_LISTA -> COMANDO COMANDO_LISTA

COMANDO_EXPRESSAO -> TK_PONTO_VIRG
COMANDO_EXPRESSAO -> EXPRESSAO TK_PONTO_VIRG

COMANDO_ITERACAO -> TK_WHILE TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR COMANDO_BLOCO
COMANDO_ITERACAO -> TK_DO COMANDO_BLOCO TK_WHILE TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR
COMANDO_ITERACAO -> TK_FOR TK_ABRE_PAR COMANDO_ITERACAO_COMANDO_FOR COMANDO_EXPRESSAO TK_FECHAR_PAR COMANDO_BLOCO
COMANDO_ITERACAO -> TK_FOR TK_ABRE_PAR COMANDO_ITERACAO_COMANDO_FOR COMANDO_EXPRESSAO EXPRESSAO TK_FECHAR_PAR COMANDO_BLOCO
COMANDO_ITERACAO_COMANDO_FOR -> COMANDO_EXPRESSAO
COMANDO_ITERACAO_COMANDO_FOR -> DEFINICAO_VARIAVEIS

COMANDO_PULO -> TK_RETURN COMANDO_EXPRESSAO
COMANDO_PULO -> TK_CONTINUE TK_PONTO_VIRG
COMANDO_PULO -> TK_BREAK TK_PONTO_VIRG

COMANDO_CONDICAO -> TK_IF TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR COMANDO_BLOCO
COMANDO_CONDICAO -> TK_IF TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR COMANDO_BLOCO TK_ELSE COMANDO_BLOCO
COMANDO_CONDICAO -> TK_SWITCH TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR COMANDO_CONDICAO_SWITCH_BLOCO
COMANDO_CONDICAO_SWITCH_BLOCO -> COMANDO_CONDICAO_CASE
COMANDO_CONDICAO_SWITCH_BLOCO -> TK_ABRE_CHAVE COMANDO_CONDICAO_LISTA_CASE TK_FECHA_CHAVE
COMANDO_CONDICAO_LISTA_CASE -> COMANDO_CONDICAO_CASE
COMANDO_CONDICAO_LISTA_CASE -> COMANDO_CONDICAO_CASE COMANDO_CONDICAO_LISTA_CASE
COMANDO_CONDICAO_CASE -> TK_CASE TK_CONST TK_DOIS_PONTOS COMANDO_BLOCO
COMANDO_CONDICAO_CASE -> TK_DEFAULT TK_DOIS_PONTOS COMANDO_BLOCO

COMANDO_FUNCAO -> CHAMADA_FUNCAO TK_PONTO_VIRG
CHAMADA_FUNCAO -> TK_ID TK_ABRE_PAR TK_FECHAR_PAR
CHAMADA_FUNCAO -> TK_ID TK_ABRE_PAR CHAMADA_FUNCAO_LISTA_PARAM TK_FECHAR_PAR
CHAMADA_FUNCAO_LISTA_PARAM -> EXPRESSAO
CHAMADA_FUNCAO_LISTA_PARAM -> EXPRESSAO TK_VIRGULA COMANDO_FUNCAO_LISTA_PARAM

EXPRESSAO -> EXPRESSAO_CONDICIONAL

EXPRESSAO_CONDICIONAL -> EXPRESSAO_LOGICA_OU
EXPRESSAO_CONDICIONAL -> TK_ABRE_PAR EXPRESSAO_LOGICA_OU TK_INTERROGACAO EXPRESSAO TK_DOIS_PONTOS EXPRESSAO_CONDICIONAL TK_FECHAR_PAR

EXPRESSAO_LOGICA_OU -> EXPRESSAO_LOGICA_E
EXPRESSAO_LOGICA_OU -> EXPRESSAO_LOGICA_OU TK_OU EXPRESSAO_LOGICA_E

EXPRESSAO_LOGICA_E -> EXPRESSAO_BITABIT_OU
EXPRESSAO_LOGICA_E -> EXPRESSAO_LOGICA_E TK_E EXPRESSAO_BITABIT_OU

EXPRESSAO_BITABIT_OU -> EXPRESSAO_BITABIT_XOU
EXPRESSAO_BITABIT_OU -> EXPRESSAO_BITABIT_OU TK_BITWISE_OU EXPRESSAO_BITABIT_XOU

EXPRESSAO_BITABIT_XOU -> EXPRESSAO_BITABIT_E
EXPRESSAO_BITABIT_XOU -> EXPRESSAO_BITABIT_XOU TK_BITWISE_XOU EXPRESSAO_BITABIT_E

EXPRESSAO_BITABIT_E	-> EXPRESSAO_IGUALDADE
EXPRESSAO_BITABIT_E	-> EXPRESSAO_BITABIT_E TK_BITWISE_E EXPRESSAO_IGUALDADE

EXPRESSAO_IGUALDADE -> EXPRESSAO_RELACIONAL
EXPRESSAO_IGUALDADE -> EXPRESSAO_IGUALDADE TK_IGUAL EXPRESSAO_RELACIONAL
EXPRESSAO_IGUALDADE -> EXPRESSAO_IGUALDADE TK_DIFERENTE EXPRESSAO_RELACIONAL

EXPRESSAO_RELACIONAL -> EXPRESSAO_BITABIT_TROCA
EXPRESSAO_RELACIONAL -> EXPRESSAO_RELACIONAL TK_MAIOR EXPRESSAO_BITABIT_TROCA
EXPRESSAO_RELACIONAL -> EXPRESSAO_RELACIONAL TK_MENOR EXPRESSAO_BITABIT_TROCA
EXPRESSAO_RELACIONAL -> EXPRESSAO_RELACIONAL TK_MAIOR_IGUAL EXPRESSAO_BITABIT_TROCA
EXPRESSAO_RELACIONAL -> EXPRESSAO_RELACIONAL TK_MENOR_IGUAL EXPRESSAO_BITABIT_TROCA

EXPRESSAO_BITABIT_TROCA -> EXPRESSAO_SOMA
EXPRESSAO_BITABIT_TROCA -> EXPRESSAO_BITABIT_TROCA TK_BITWISE_ESQ EXPRESSAO_SOMA
EXPRESSAO_BITABIT_TROCA -> EXPRESSAO_BITABIT_TROCA TK_BITWISE_DIR EXPRESSAO_SOMA

EXPRESSAO_SOMA -> EXPRESSAO_MULT
EXPRESSAO_SOMA -> EXPRESSAO_SOMA TK_SOMA EXPRESSAO_MULT
EXPRESSAO_SOMA -> EXPRESSAO_SOMA TK_SUB EXPRESSAO_MULT

EXPRESSAO_MULT -> EXPRESSAO_ATRIB
EXPRESSAO_MULT -> EXPRESSAO_MULT TK_MULT EXPRESSAO_ATRIB
EXPRESSAO_MULT -> EXPRESSAO_MULT TK_DIV EXPRESSAO_ATRIB
EXPRESSAO_MULT -> EXPRESSAO_MULT TK_RESTO EXPRESSAO_ATRIB

EXPRESSAO_ATRIB -> EXPRESSAO_UNARIA
EXPRESSAO_ATRIB -> TK_ID OPERADOR_ATRIB EXPRESSAO_ATRIB

EXPRESSAO_UNARIA -> EXPRESSAO_PRIMARIA
EXPRESSAO_UNARIA -> TK_INCREMENTO TK_ID
EXPRESSAO_UNARIA -> TK_DECREMENTO TK_ID
EXPRESSAO_UNARIA -> TK_ID TK_INCREMENTO
EXPRESSAO_UNARIA -> TK_ID TK_DECREMENTO
EXPRESSAO_UNARIA -> OPERADOR_UNARIO EXPRESSAO_PRIMARIA

EXPRESSAO_PRIMARIA -> TK_CONST
EXPRESSAO_PRIMARIA -> TK_ID
EXPRESSAO_PRIMARIA -> TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR
EXPRESSAO_PRIMARIA -> CHAMADA_FUNCAO

OPERADOR_ATRIB -> TK_ATRIB
OPERADOR_ATRIB -> TK_INCR_MULT
OPERADOR_ATRIB -> TK_INCR_DIV
OPERADOR_ATRIB -> TK_INCR_RESTO
OPERADOR_ATRIB -> TK_INCR_SOMA
OPERADOR_ATRIB -> TK_INCR_SUB

OPERADOR_UNARIO -> TK_BITWISE_E
OPERADOR_UNARIO -> TK_BITWISE_COMPL
OPERADOR_UNARIO -> TK_SOMA
OPERADOR_UNARIO -> TK_SUB
OPERADOR_UNARIO -> TK_NEGACAO

TK_VIRGULA -> ,
TK_ABRE_PAR -> (
TK_FECHAR_PAR -> )
TK_DOIS_PONTOS -> :
TK_ATRIB -> =
TK_PONTO_VIRG -> ;
TK_ABRE_CHAVE -> {
TK_FECHA_CHAVE -> }
TK_MULT -> *
TK_DIV -> /
TK_RESTO -> %
TK_SOMA -> +
TK_SUB -> -
TK_INCREMENTO -> ++
TK_DECREMENTO -> --
TK_MAIOR -> >
TK_MENOR -> <
TK_MENOR_IGUAL -> <=
TK_MAIOR_IGUAL -> >=
TK_IGUAL -> ==
TK_DIFERENTE -> !=
TK_INCR_MULT -> *=
TK_INCR_DIV -> /=
TK_INCR_RESTO -> %=
TK_INCR_SOMA -> +=
TK_INCR_SUB -> -=
TK_E -> &&
TK_OU -> ||
TK_NEGACAO -> !
TK_WHILE -> while
TK_DO -> do
TK_FOR -> for
TK_CONTINUE -> continue
TK_IF -> if
TK_ELSE -> else
TK_SWITCH -> switch
TK_CASE -> case
TK_DEFAULT -> default
TK_BREAK -> break
TK_ID -> id
TK_VOID -> void
TK_INT -> int
TK_FLOAT -> float
TK_DOUBLE -> double
TK_CONST -> 1
TK_BITWISE_E -> &
TK_BITWISE_OU -> |
TK_BITWISE_XOU -> ^
TK_BITWISE_COMPL -> ~
TK_BITWISE_ESQ -> <<
TK_BITWISE_DIR -> >>
TK_RETURN -> return
TK_INTERROGACAO -> ?