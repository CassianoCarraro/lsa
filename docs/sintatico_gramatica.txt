INICIO -> DEFINICAO_FUNCAO
		| DEFINICAO_VARIAVEIS

TIPO -> TK_VOID
		| TK_INT
		| TK_FLOAT
		| TK_DOUBLE

DEFINICAO_FUNCAO -> TIPO TK_ID TK_ABRE_PAR DEFINICAO_PARAMETROS TK_FECHAR_PAR DEFINICAO_FUNCAO_CORPO
				| TIPO TK_ID TK_ABRE_PAR TK_FECHAR_PAR DEFINICAO_FUNCAO_CORPO
DEFINICAO_FUNCAO_CORPO -> TK_PONTO_VIRG
						| COMANDO

DEFINICAO_PARAMETROS -> DEFINICAO_PARAMETRO
					| DEFINICAO_PARAMETRO TV_VIRGULA DEFINICAO_PARAMETROS
DEFINICAO_PARAMETRO -> TIPO TK_ID

DEFINICAO_VARIAVEIS -> TIPO DEFINICAO_VARIAVEL_LISTA TK_PONTO_VIRG
DEFINICAO_VARIAVEIS_LISTA -> DEFINICAO_VARIAVEL
							| DEFINICAO_VARIAVEL TK_VIRGULA DEFINICAO_VARIAVEIS_LISTA
DEFINICAO_VARIAVEL -> TK_ID
					| TK_ID TK_ATRIB EXPRESSAO

COMANDO -> COMANDO_BLOCO
		| COMANDO_ITERACAO
		| COMANDO_EXPRESSAO
		| COMANDO_PULO
		| COMANDO_FUNCAO

COMANDO_BLOCO -> COMANDO
				| TK_ABRE_CHAVE TK_FECHA_CHAVE
				| TK_ABRE_CHAVE COMANDO_LISTA TK_FECHA_CHAVE

COMANDO_LISTA -> COMANDO
				| COMANDO COMANDO_LISTA

COMANDO_EXPRESSAO -> TK_PONTO_VIRG
					| EXPRESSAO TK_PONTO_VIRG

COMANDO_ITERACAO -> TK_WHILE TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR COMANDO_ITERACAO_COMANDO
				| TK_DO COMANDO_ITERACAO_COMANDO TK_WHILE TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR
				| TK_FOR TK_ABRE_PAR COMANDO_EXPRESSAO COMANDO_EXPRESSAO TK_FECHAR_PAR COMANDO_ITERACAO_COMANDO
				| TK_FOR TK_ABRE_PAR COMANDO_EXPRESSAO COMANDO_EXPRESSAO EXPRESSAO TK_FECHAR_PAR COMANDO_ITERACAO_COMANDO
COMANDO_ITERACAO_COMANDO -> COMANDO
						| COMANDO_ITERACAO_PULO
COMANDO_ITERACAO_PULO -> TK_CONTINUE TK_PONTO_VIRG
						| TK_BREAK TK_PONTO_VIRG

COMANDO_PULO -> TK_RETORNO COMANDO_EXPRESSAO

COMANDO_CONDICAO -> TK_IF TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR COMANDO
				| TK_IF TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR COMANDO TK_ELSE COMANDO
				| TK_SWITCH TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR COMANDO_CONDICAO_LISTA_CASE
COMANDO_CONDICAO_LISTA_CASE -> COMANDO_CONDICAO_CASE
							| COMANDO_CONDICAO_CASE COMANDO_CONDICAO_LISTA_CASE
COMANDO_CONDICAO_CASE -> TK_CASE TK_CONST TK_DOIS_PONTOS COMANDO_CONDICAO_CASE_COMANDO
						| TK_DEFAULT TK_DOIS_PONTOS COMANDO_CONDICAO_CASE_COMANDO
COMANDO_CONDICAO_CASE_COMANDO -> COMANDO
								| TK_BREAK TK_PONTO_VIRG

COMANDO_FUNCAO -> TK_ID TK_ABRE_PAR COMANDO_FUNCAO_LISTA_PARAM TK_FECHAR_PAR
COMANDO_FUNCAO_LISTA_PARAM -> EXPRESSAO
							| EXPRESSAO TK_VIRGULA COMANDO_FUNCAO_LISTA_PARAM

EXPRESSAO -> EXPRESSAO_LOGICA_OU

EXPRESSAO_LOGICA_OU -> EXPRESSAO_LOGICA_E
					| EXPRESSAO_LOGICA_OU TK_OU EXPRESSAO_LOGICA_E

EXPRESSAO_LOGICA_E -> EXPRESSAO_BITABIT_OU
					| EXPRESSAO_LOGICA_E TK_E EXPRESSAO_BITABIT_OU

EXPRESSAO_BITABIT_OU -> EXPRESSAO_BITABIT_XOU
					| EXPRESSAO_BITABIT_OU TK_BITWISE_OU EXPRESSAO_BITABIT_XOU

EXPRESSAO_BITABIT_XOU -> EXPRESSAO_BITABIT_E
					| EXPRESSAO_BITABIT_XOU TK_BITWISE_XOU EXPRESSAO_BITABIT_E

EXPRESSAO_BITABIT_E	-> EXPRESSAO_IGUALDADE
					| EXPRESSAO_BITABIT_E TK_BITWISE_E EXPRESSAO_IGUALDADE

EXPRESSAO_IGUALDADE -> EXPRESSAO_RELACIONAL
					| EXPRESSAO_IGUALDADE TK_IGUAL EXPRESSAO_RELACIONAL
					| EXPRESSAO_IGUALDADE TK_DIFERENTE EXPRESSAO_RELACIONAL

EXPRESSAO_RELACIONAL -> EXPRESSAO_BITABIT_TROCA
					| EXPRESSAO_RELACIONAL TK_MAIOR EXPRESSAO_BITABIT_TROCA
					| EXPRESSAO_RELACIONAL TK_MENOR EXPRESSAO_BITABIT_TROCA
					| EXPRESSAO_RELACIONAL TK_MAIOR_IGUAL EXPRESSAO_BITABIT_TROCA
					| EXPRESSAO_RELACIONAL TK_MENOR_IGUAL EXPRESSAO_BITABIT_TROCA

EXPRESSAO_BITABIT_TROCA -> EXPRESSAO_SOMA
						| EXPRESSAO_BITABIT_TROCA TK_BITWISE_ESQ EXPRESSAO_SOMA
						| EXPRESSAO_BITABIT_TROCA TK_BITWISE_DIR EXPRESSAO_SOMA

EXPRESSAO_SOMA -> EXPRESSAO_MULT
				| EXPRESSAO_SOMA TK_SOMA EXPRESSAO_MULT
				| EXPRESSAO_SOMA TK_SUB EXPRESSAO_MULT

EXPRESSAO_MULT -> EXPRESSAO_ATRIB
				| EXPRESSAO_MULT TK_MULT EXPRESSAO_ATRIB
				| EXPRESSAO_MULT TK_DIV EXPRESSAO_ATRIB
				| EXPRESSAO_MULT TK_RESTO EXPRESSAO_ATRIB

EXPRESSAO_ATRIB -> EXPRESSAO_UNARIA
				| TK_ID OPERADOR_ATRIB EXPRESSAO_ATRIB

EXPRESSAO_UNARIA -> EXPRESSAO_PRIMARIA
				| TK_INCREMENTO TK_ID
				| TK_DECREMENTO TK_ID
				| TK_ID TK_INCREMENTO
				| TK_ID TK_DECREMENTO
				| OPERADOR_UNARIO EXPRESSAO_PRIMARIA

EXPRESSAO_PRIMARIA -> TK_CONST
					| TK_ID
					| TK_ABRE_PAR EXPRESSAO TK_FECHAR_PAR

OPERADOR_ATRIB -> TK_ATRIB
				| TK_INCR_MULT
				| TK_INCR_DIV
				| TK_INCR_RESTO
				| TK_INCR_SOMA
				| TK_INCR_SUB

OPERADOR_UNARIO -> TK_BITWISE_E
				| TK_BITWISE_COMPL
				| TK_SOMA
				| TK_SUB
				| TK_NEGACAO